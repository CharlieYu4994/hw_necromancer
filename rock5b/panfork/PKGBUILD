_commit=120202c675749c5ef81ae4c8cdc30019b4de08f4
_fwcommit=309268f7a34ca0bba0ab94a0b09feb0191c77fb8
pkgname=panfork
pkgdesc="Libgl & gbm with panfrost for Mali G610"
pkgver=23.0.0
pkgrel=2
arch=('aarch64')

makedepends=( 'meson' 'python-mako' 'expat' 'bison' 'flex' 'libxext' 'libxfixes' 'libxcb'
              'libxshmfence' 'libxxf86vm' 'libxrandr' 'cmake' 'libdrm' 'egl-wayland' )
url="https://gitlab.com/panfork/mesa"
license=('custom')
options=('!lto')
source=("https://gitlab.com/panfork/mesa/-/archive/${_commit}/mesa-${_commit}.tar.gz"
		"https://raw.githubusercontent.com/JeffyCN/rockchip_mirrors/${_fwcommit}/firmware/g610/mali_csffw.bin"
        LICENSE)
sha512sums=('SKIP'
            'SKIP'
            'SKIP')

build() {
  arch-meson mesa-$_commit build \
    -Dgallium-drivers=panfrost \
    -Dvulkan-drivers= \
    -Dllvm=disabled \
    -Dmicrosoft-clc=disabled \
    -Dintel-clc=disabled \
    -Dplatforms=wayland,x11

  ninja -C build
  meson compile -C build

  # fake installation to be seperated into packages
  # outside of fakeroot but mesa doesn't need to chown/mod
  DESTDIR="${srcdir}/fakeinstall" meson install -C build
}

package_panfork() {
  provides=('libgl' 'libegl' 'libgles' 'opengl-driver' 'mesa' 'libglvnd')
  conflicts=('libgl' 'mesa' 'libglvnd')
  replaces=('libgl' 'mesa' 'libglvnd')

  # copy firmware
  install -m644 -Dt "$pkgdir/usr/lib/firmware/" mali_csffw.bin
  
  #install libs
  _install fakeinstall/usr/share/drirc.d/00-mesa-defaults.conf
  _install fakeinstall/usr/lib/dri/*_dri.so
  _install fakeinstall/usr/lib/lib{gbm,glapi}.so*
  _install fakeinstall/usr/lib/libGL*
  _install fakeinstall/usr/lib/libEG*
  _install fakeinstall/usr/include
  _install fakeinstall/usr/lib/pkgconfig
  # make sure there are no files left to install
  find fakeinstall -depth -print0 | xargs -0 rmdir

  install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}

_install() {
  local src f dir
  for src; do
    f="${src#fakeinstall/}"
    dir="${pkgdir}/${f%/*}"
    install -m755 -d "${dir}"
    mv -v "${src}" "${dir}/"
  done
}